buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:0.12.613'
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.0.23"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

intellij {
    version '14.1.4'
    plugins 'kotlin'
}

sourceSets {

    main {
        kotlin {
            srcDirs = ['src']
        }
        java {
            srcDirs = ['src', 'gen']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }

    test {
        kotlin {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:0.12.613'
    compile files('format.jar')
    compile files('printer-core.jar')
    testCompile 'junit:junit:+'
}

idea.project.ipr {
    beforeMerged { project ->
        project.modulePaths.clear()
    }
}

idea.module {
    name = "whileLang"
    jdkName = "IntelliJ IDEA SDK"
    sourceDirs += file('src')
    sourceDirs += file('gen')
    testSourceDirs += file('test')
    iml {
        withXml {
            it.node.@type = "PLUGIN_MODULE"
            it.node.appendNode('component'
                    , [name: 'DevKit.ModuleBuildProperties', url: 'file://$MODULE_DIR$/src/resources/META-INF/plugin.xml'])
        }
    }
}

task setup {
    dependsOn ideaModule, ideaProject
    doLast {
        copy {
            from '.'
            into '.idea/'
            include '*.ipr'
            rename { "modules.xml" }
        }
        project.delete "${project.name}.ipr"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

